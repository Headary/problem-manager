@precedence { commandargument @left, character @left }

@top Document { (Text | InlineMath | Command | EqCommand | "[" | "]" | "{" | "}" | Newline)* }

// MathCommandArgument is included to catch { math } groups
Math {
	!character (
		InlineMathContent |
		Command |
		UnderscoreCommand |
		MathCommandArgument |
		QuoteMacro |
		"[" | "]" | "{" | "}" |
		Whitespace |
		Number |
		Newline
	)*
}

InlineMath {
	"$" Math "$"
}

Command {
	CommandIdentifier (CommandArgument | CommandArgumentOptional)*
}

// command argument is included to catch { } groups
CommandArgument {
	!commandargument "{" (Text | InlineMath | Command | CommandArgument | Newline )* "}"
}

MathCommandArgument {
	!commandargument "{" Math "}"
}

CommandArgumentOptional {
	!commandargument "[" (Text | InlineMath | Command | Newline)* "]"
}

EqCommandIdentifier {
	@specialize<CommandIdentifier, "\\eq">
}

EqCommand {
	 EqCommandIdentifier CommandArgumentOptional? MathCommandArgument
}

UnderscoreCommand {
	@specialize<CommandIdentifier, "\\_"> (CommandArgument|Command|Singlechar)
}

QuoteMacro {
	"\"" Number ("e" Number)? (Whitespace Math)? "\""
}

@skip { Comment }

@tokens {
	Text { (![\\${}\[\]%\n])+ }
	CommandIdentifier { "\\" ((@asciiLetter)+"*"?|"%"|"{"|"}"|"\\"|"_"|"("|")"|",") }
	InlineMathContent { (![\\${}\[\]\"%\n\t\r 0-9])+ }
	Comment { "%" ![\n]* }
	Newline { "\n" }
	Singlechar { @asciiLetter }
	Whitespace { ("\t"|"\r"|" ")+ }
	Number { ("+" | "-")? @digit+ ("~" @digit+)* ("," | ".")? @digit* ("~" @digit+)* }

	// listing those characters to enable syntax highlighting
	"[" "]" "{" "}" "$" "\"" "e"

	@precedence {Number, InlineMathContent}
}
